apply plugin: "maven-publish"

def LIB_GROUP_ID = "com.raywenderlich.notktx"
def LIB_ARTIFACT_ID = "$rootProject.name-$project.name"
// DONE:12 Update library version
def LIB_VERSION = "1.0.2"

afterEvaluate {
  // Publishing only the library modules
  if (plugins.hasPlugin("android-library")) {
    publishing {
      publications {
        release(MavenPublication) {
          groupId LIB_GROUP_ID
          artifactId LIB_ARTIFACT_ID
          version LIB_VERSION

          artifact("$buildDir/outputs/aar/${project.name}-release.aar")

          pom.withXml {
            def dependenciesNode = asNode().appendNode("dependencies")
            // Iterate over the compile dependencies (we don't want the test ones), adding a
            // <dependency> node for each
            configurations.api.allDependencies.each {
              def dependencyNode = dependenciesNode.appendNode("dependency")
              dependencyNode.appendNode("groupId", it.group)
              dependencyNode.appendNode("artifactId", it.name)
              dependencyNode.appendNode("version", it.version)
            }
          }
        }
      }
    }
  }
}